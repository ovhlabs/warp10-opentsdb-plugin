import java.text.SimpleDateFormat

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'warp10-opentsdb-plugin'
def pushgatewayVersion = '1.0.0'
version = pushgatewayVersion
group = 'io.warp10'

sourceCompatibility = 1.7
targetCompatibility = 1.7

//
// Repositories for dependency resolution
//
repositories {
    jcenter()
    mavenCentral()
    mavenLocal()

    maven {
        url 'https://dl.bintray.com/cityzendata/maven'
    }

    maven {
        url 'https://dl.bintray.com/cityzendata/generic'
    }

    maven {
        url 'https://dl.bintray.com/hbs/maven'
    }

    // bedatadriven - http://www.renjin.org/#downloads
    maven {
        url "http://nexus.bedatadriven.com/content/groups/public/"
    }
}

configurations {
    provided
}

dependencies {
    provided group:'io.warp10', name: 'warpscript', version: '1.2.2-20-g992b83f'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.0.5.v20130815'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '9.0.5.v20130815'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'io.warp10.plugins.opentsdb.PushGatewayHandler'
        )
    }
    relocate 'org', 'io.warp10.quantum.org'
    relocate 'jetty', 'io.warp10.pushgateway.jetty'
    relocate 'javax', 'io.warp10.quantum.javax'
    classifier = null
}

artifacts {
    archives shadowJar
}